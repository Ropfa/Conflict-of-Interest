label top
bg police

examine
region 171 83 73 75 computers
region 8 69 97 77 computers
region 159 54 73 16 badgers
region 130 103 145 112 badgers
region 120 51 21 26 chief
menu scene63

label computers
char phoenix hide
"All the police are hard at work, {n}solving various crimes."
char Chief hide
"Hey, stop {sfx shout-general}that!!"
"It's no fun when the other team {n}spawn camps!"
char phoenix hide
"...{p20}Except that one."
goto top

label badgers
char phoenix hide
"The Blue Badger..."
char maya e=cheerful
"And his wife {p15}- The Pink Badger!"
char phoenix hide
"His son, the Baby Badger."
char maya e=pumped
"Don't forget their nemesis, the {n}Bad Badger!"
char phoenix hide
"And finally, that creepy thing lying on {n}the floor over there."
"What was that called again?"
char maya e=normal
"The Proto Badger! {n}{e sad}I remember having nightmares about {n}that one."
char phoenix hide
"You know, I think that every time we {n}come here, they have more of those {n}things."
goto top

label chief
char Chief hide
"Hey, you!"
char phoenix hide
"Who, me?"
char Chief hide
"Yeah, you! {p30}{n}Don't do drugs!"
char phoenix hide
"...{p15}Yes, sir."
goto top

label none
char phoenix hide
"No clues here."
goto top