set _list_back_button false
set wrongevidence 1

mus
bg witness
fg wbench
char watters e=smirk
fg TestimonyAnim fade name=tanim
nt
delete name=tanim
"{n}{center}{c940}-- No Way, Man! --"

mus Confrontation - Moderato.ogg
set _music_loop Confrontation - Moderato - loop.ogg
bg witness
fg wbench

char watters e=smirk
"{tbon}Just because there was a blackout, {n}doesn't mean I went there."
char watters e=thinking
"{tbon}I mean, that tower's over thirty {n}stories tall, and the elevators {n}woulda been out."
"{tbon}No way am I gonna run up and down {n}something like that."
char watters e=serious
"{tbon}Plus, I don't exactly want to get {n}involved with the mob."

mus
bg black
pause 30
bg prosecution
fg pbench
char obadiah e=serious
"This is a very serious accusation {n}you're making here. I hope you can {n}back up what you're saying."
bg witness
fg wbench
char watters e=smirk
"Ya hear that, man? {n}Boss's got my back!"
bg defense
fg dbench
char phoenix e=handsondesk
"{c089}(I don't care who has your back here. {n}You can't escape from what you've {n}done, Watters!)"

bg witness
fg wbench
char watters e=smirk
bg main name=main y=192 stack
fg phoenixvs name=phoenixvs x=-255 y=165 stack
fg williamsonvs name=willvs x=256 y=225 stack
scroll name=phoenixvs x=255 speed=8 nowait
scroll name=willvs x=-256 speed=8 nowait
fg Cross-ExaminationAnim stack fade
pause 20
delete name=phoenixvs
delete name=willvs
delete name=main
nt
"{n}{center}{c940}-- No Way, Man! --"
set show_watters1C true
set show_watters1C1 false
mus Confrontation - Moderato.ogg
set _music_loop Confrontation - Moderato - loop.ogg
label start
cross watterstestimony1

statement watters1A
bg witness
fg wbench
char watters e=smirk
"Just because there was a blackout, {n}doesn't mean I went there."

statement watters1B
bg witness
fg wbench
char watters e=thinking
"I mean, that tower's over thirty {n}stories tall, and the elevators {n}woulda been out."

statement watters1C test=show_watters1C
bg witness
fg wbench
char watters e=thinking
"No way am I gonna run up and down {n}something like that."

statement watters1C1 test=show_watters1C1
bg witness
fg wbench
char watters e=serious
"I'd have to have a really good reason {n}to run up and down sixty flights of {n}stairs."

statement watters1D
bg witness
fg wbench
char watters e=serious
"Plus, I don't exactly want to get {n}involved with the mob."
endcross

label comments
bg aide
char maya e=bench
"Do you see anything wrong with his {n}testimony, Nick?"
char phoenix hide
"He's claiming that he didn't have any {n}reason to go there, which I know is {n}wrong."
"I think I've finally found out what {n}{c940}a certain piece of evidence{c999} {n}means."
char maya e=bench
"Well, then show it! {n}You know what to do!"
goto start

label press watters1A
sfx /phoenix/holdit.ogg
fg HoldItAnim
pause 5 script
bg defense
fg dbench
char phoenix e=normal
"If you weren't there, then where {n}were you?"
bg witness
fg wbench
char watters e=angry
"Hey, that doesn't matter!"
bg defense
fg dbench
char phoenix e=thinking
"I think it does. If you can't prove {n}where you were, then you don't have {n}an alibi."
bg witness
fg wbench
char watters e=angry
"I... {p15}Well, I was at home, okay? {n}You know, like any normal person {n}would be?"
bg defense
fg dbench
char phoenix e=deskslam
sfx deskslam
pause 50 script
char phoenix e=handsondesk
""I was at home" isn't an alibi!"
sfx /godot/objection.ogg
fg ObjectionAnim
pause 5 script
bg prosecution
fg pbench
char obadiah e=serious
"I want proof. {p25}{n}Proof that he was there when {n}the murder took place."
"Until you give me that, it doesn't {n}matter where he says he was or {n}wasn't."
bg witness
fg wbench
char watters e=smirk
"Yeah! That's right!"
resume

label press watters1B
sfx /phoenix/holdit.ogg
fg HoldItAnim
pause 5 script
bg defense
fg dbench
char phoenix e=thinking
"Why do you say that?"
bg witness
fg wbench
char watters e=angry
"No power, no elevators, right?"
"Duh. {p15}You're the one who brought {n}that up."
bg defense
fg dbench
char phoenix e=thinking
"Right..."
bg witness
fg wbench
char watters e=serious
"The only other way would be the {n}stairs."
"Even I'm not crazy enough to try {n}that."
bg judge
char judge e=normal
"Thirty flights of stairs would tire you {n}out rather quickly."
char judge e=surprised
"Why, if I were to try it, I think I would {n}pass out!"
bg witness
fg wbench
char watters e=serious
"Yeah, no kidding!"
"I mean, come on..."
resume

label press watters1C
sfx /phoenix/holdit.ogg
fg HoldItAnim
pause 5 script
bg defense
fg dbench
char phoenix e=normal
"Whoever said anything about {n}running? {p15}You could've walked."
bg witness
fg wbench
char watters e=angry
"You're kinda missing the point. I'm {n}not gonna waste my time going up {n}and down those stairs."
"It would be bad enough going up, but {n}then I'd have to go all the way back {n}down again."
bg defense
fg dbench
char phoenix e=normal
"The blackout was the only {n}opportunity to safely infiltrate {n}the building."
"Taking the stairs was a necessary {n}sacrifice."
sfx /godot/objection.ogg
fg ObjectionAnim
pause 5 script
bg prosecution
fg pbench
char obadiah e=serious
"That would, of course, require him {n}to have known about the power {n}outage beforehand."
"I don't suppose that you have {n}evidence showing that the power {n}failure was planned."
bg defense
fg dbench
char phoenix e=thinking
"N-No. I don't."
bg prosecution
fg pbench
char obadiah e=serious
"{sfx shout-general}Ha!"
bg witness
fg wbench
char watters e=smirk
"Yeah! Ha!"
bg defense
fg dbench
char phoenix e=handsondesk
"{c089}(Don't get cocky, Watters. {n}This is just round one.)"
bg witness
fg wbench
char watters e=smirk
"Since I didn't know it was gonna {n}happen, that means if I did show up {n}there..."
"...I would've planned to use the {n}elevator instead."
"I would've needed a really important {n}reason to still go up there, {n}after seeing that it was out."
bg defense
fg dbench
char phoenix e=normal
"So if you DID have a good reason, {n}you would do it?"
bg witness
fg wbench
char watters e=angry
"W-Well, yeah... {p10}I guess."
bg defense
fg dbench
char phoenix e=normal
"Maybe you should change your {n}testimony to reflect that then."
bg witness
fg wbench
char watters e=angry
"Y-Yeah, okay."
set show_watters1C false
set show_watters1C1 true
resume

label press watters1C1
sfx /phoenix/holdit.ogg
fg HoldItAnim
pause 5 script
bg defense
fg dbench
char phoenix e=handsondesk
"Murder isn't a good enough reason {n}for you?"
bg witness
fg wbench
char watters e=nervous
"Hey, I didn't murder anyone!"
sfx /godot/objection.ogg
fg ObjectionAnim
pause 5 script
bg prosecution
fg pbench
char obadiah e=serious
"Stop badgering him!"
"You've done nothing to prove {n}anything yet."
bg judge
char judge e=nodding
pause 50 script
char judge e=normal
"Objection sustained."
bg defense
fg dbench
char phoenix e=handsondesk
"{c089}(This isn't getting me anywhere.)"
"{c089}(I'll need to actually show them my {n}proof if I want to make progress.)"
resume

label press watters1D
sfx /phoenix/holdit.ogg
fg HoldItAnim
pause 5 script
bg defense
fg dbench
char phoenix e=normal
"Who mentioned anything about {n}getting involved with the mob?"
bg witness
fg wbench
char watters e=angry
"Well, uh... {p10}Isn't that the only reason {n}anybody would show up there?"
"Especially at that time of day."
bg defense
fg dbench
char phoenix e=thinking
"I guess I can't really think of any {n}other reasons myself."
bg witness
fg wbench
char watters e=angry
"You don't want to get messed up {n}with those guys."
"They'll ruin you."
char phoenix hide
"{c089}(I guess there really isn't any arguing {n}with that.)"
resume

//PRESENT BLACKMAIL LETTER

label blackmail2 watters1C1
mus
sfx /phoenix/objection.ogg
fg ObjectionAnim
pause 5 script
bg defense
fg dbench
char phoenix e=normal
"What if you did have a good reason?"
"What if there was a reason that you {n}couldn't refuse?"
bg witness
fg wbench
char watters e=nervous
"Waddaya mean?{n}{p20}Like what?"
bg defense
fg dbench
char phoenix e=confident
"What if... you were being {n}blackmailed?"
mus Objection.ogg
set _music_loop Objection - loop.ogg
bg witness
fg wbench
char watters e=nervous
"Black-{s5}{p 5}{sfx shout-shock}{e shock}what?"
bg defense
fg dbench
char phoenix e=confident
"You heard me."
set _ev blackmail
showevr
"When we found this letter, I {n}automatically assumed that Rivales {n}was its target."
"But what if that wasn't the case?"
bg witness
fg wbench
char watters e=nervous
"Come on! Of course it was for him! {n}We found it in his office!"
bg defense
fg dbench
char phoenix e=confident
"Are you saying that there's {n}somebody out there..."
"...who was actually stupid {n}enough to try and blackmail {n}a crime boss?"
bg witness
fg wbench
char watters e=nervous
"{sfx idea}!!"
bg defense
fg dbench
char phoenix e=confident
"The note we found was nothing more {n}than a copy Rivales kept for {n}himself."
"I found an identical note yesterday... {n}The one I'm sure belonged to the real {n}target!"
sfx /godot/objection.ogg
fg ObjectionAnim
pause 5 script
bg prosecution
fg pbench
char obadiah e=serious
"What, might I ask, was he {n}supposedly being blackmailed over?"
"It would have to be something {n}serious."
bg witness
fg wbench
char watters e=nervous
"Hey, I've got nothing to hide, man!"
bg defense
fg dbench
char phoenix e=handsondesk
"{c089}(I've come this far, but I still know so {n}little.)"
"{c089}(I still don't have any idea what this {n}"big secret" the note talked about {n}was.)"
mus
"I... {p10}I don't know."
bg witness
fg wbench
char watters e=smirk
"{sfx shout-general}HA{sfx shout-general}HA!!{n}{p20}You hear that, Boss?{n}He doesn't know!"
bg prosecution
fg pbench
char obadiah e=serious
"Yes, I can hear just fine."
"The existence of a copy of the letter {n}doesn't point to the witness at all."
bg defense
fg dbench
char phoenix e=normal
"Maybe not."
char phoenix e=confident
"But I can prove that he responded to {n}it."
bg prosecution
fg pbench
char obadiah e=serious
"{sfx idea}!!"
char obadiah e=serious2
"..."
bg defense
fg dbench
char phoenix e=normal
"Detective, perhaps you'd like to talk {n}to us about the blackmail note?
bg witness
fg wbench
char watters e=nervous
"I... I dunno... I..."
bg judge
char judge e=normal
"Whether or not it was directed at {n}you is a serious matter, and {n}certainly a solid motive for murder."
"This court requests further {n}testimony. I expect you to {n}give it."
bg witness
fg wbench
char watters e=angry
"...{p15}Fine."
script scene51watterstestimony2

label none
set _cr_back_button true
is wrongevidence 1 wrongevidence1
is wrongevidence 2 wrongevidence2
is wrongevidence 3 wrongevidence3

label wrongevidence1
sfx /phoenix/objection.ogg
fg ObjectionAnim
pause 5 script
bg defense
fg dbench
char phoenix e=pointing
"This evidence proves that the {n}witness is lying!"
bg prosecution
fg pbench
char obadiah e=serious
"Actually, no... {p15}{n}However, it does prove that you're {n}an idiot."
bg defense
fg dbench
char phoenix e=sweating
"Excuse me...?"
bg judge
char judge e=normal
"I think what he's trying to say is that {n}a penalty is in order here."
sfx damage.wav
penalty -15
is penalty 0 failure
bg defense
fg dbench
char phoenix e=thinking
"{c089}(That didn't quite go as planned...)"
pause 20 script
set wrongevidence 2
clear
goto start

label wrongevidence2
sfx /phoenix/objection.ogg
fg ObjectionAnim
pause 5 script
bg defense
fg dbench
char phoenix e=pointing
"This evidence proves it!"
bg prosecution
fg pbench
char obadiah e=serious
"What does it prove?"
bg defense
fg dbench
char phoenix e=thinking
"Well, it proves something..."
bg prosecution
fg pbench
char obadiah e=serious
"Yes, it proves that you're grasping {n}at straws and have no idea what {n}you're doing."
bg defense
fg dbench
char phoenix e=sweating
"That's... {p10}not..."
bg judge
char judge e=warning
"Mr. Wright! {n}There will be no straw grasping in my {n}courtroom!"
sfx damage.wav
penalty -15
is penalty 0 failure
bg defense
fg dbench
char phoenix e=sweating
"{c089}(Urk...)"
pause 20 script
set wrongevidence 3
clear 
goto start

label wrongevidence3
sfx /phoenix/objection.ogg
fg ObjectionAnim
pause 5 script
bg defense
fg dbench
char phoenix e=handsondesk
"Your Honor!{n}{p20}That statement contradicts the {n}evidence!"
bg prosecution
fg pbench
char obadiah e=serious
"No.{p20}{n}It doesn't."
bg defense
fg dbench
char phoenix e=handsondesk
"Yes, it does...{p20}{n}{e pointing}And I can prove it!"
bg prosecution
fg pbench
char obadiah e=serious
"No.{p20}{n}You can't."
bg defense
fg dbench
char phoenix e=handsondesk
"I...{n}{p20}{e sweating}No, {p20}I can't."
bg judge
char judge e=normal
"Perhaps you should think things {n}through before jumping to {n}conclusions."
"Then I wouldn't have to give you {n}penalties."
char judge e=warning
"{sfx shout-general}Like this one!"
sfx damage.wav
penalty -15
is penalty 0 failure
pause 20 script
set wrongevidence 1
clear 
goto start

label failure
mus
fg ani-judge-gavel
pause 40 script
bg judge
char judge e=normal
"That's enough."
"I see no reason to prolong this trial."
"The defense's case is insufficient to {n}overturn the prosecution's claims."
"I will now turn this case over to the {n}jury. They may decide their verdict."
bg black fade
pause 60
bg judge fade
char judge e=normal
"The jury has decided.{n}{p20}It is unanimous."
"This court finds the defendant, {n}Wayne Parker...{next}"
guilty
pause 60
char judge hide
"The defendant will submit himself to {n}the court, {p20}to be held pending {n}trial at a higher court."
"Court is adjourned."
pause 30 script
exit